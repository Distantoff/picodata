Command = _{ SOI ~ Query ~ EOF }

Query = _{ Select | UnionAll | Values | Insert }
    Select = {
        ^"select" ~ Projection ~ ^"from" ~ Scan ~
        (((^"inner" ~ ^"join") | ^"join") ~ InnerJoin)? ~
        (^"on" ~ Condition)? ~ (^"where" ~ Selection)?
    }
        Projection = { Column ~ ("," ~ Column)*? }
            Column = { Row | Asterisk | Alias | Value }
                Alias = {Value ~ ^"as" ~ (String | QuotedName) }
                Asterisk = @{ "*" }
        Selection = { Expr* }
        Scan = { SubQuery | Table }
            Table = @{ String | QuotedName }
        InnerJoin = { Scan }
        Condition = { Selection }
    UnionAll = { (SubQuery | Select) ~ ^"union" ~ ^"all" ~ (UnionAll | SubQuery | Select) }
    SubQuery = { "(" ~ (UnionAll | Select) ~ ")" ~ (^"as" ~ (String | QuotedName))? }
    Insert = { ^"insert" ~ ^"into" ~ Table ~ Values }
    Values = { ^"values" ~ Row ~ ("," ~ Row)*?}

Expr = _{  ExprOr | ExprAnd | ExprCmp | ExprPrimary | ExprParentheses }
    ExprParentheses = _{ "(" ~ Expr ~ ")" }
    ExprPrimary = _{ SubQuery | Value }
    ExprCmp = { ExprCmpLeft ~ Cmp ~ ExprCmpRight }
        ExprCmpLeft = _{ ExprPrimary | ExprParentheses }
        Cmp = _{ In | NotEq | GtEq | LtEq | Eq | Gt | Lt }
            Eq = @{ "=" }
            In = @{ ^"in" }
            Gt = @{ ">" }
            GtEq = @{ ">=" }
            Lt = @{ "<" }
            LtEq = @{ "<=" }
            NotEq = @{ "!=" | "<>" }
        ExprCmpRight = _{ ExprCmp | ExprCmpLeft }
    ExprAnd = { ExprAndLeft ~ And ~ ExprAndRight }
        ExprAndLeft = _{ ExprCmpRight }
        And = @{ ^"and" }
        ExprAndRight = _{ ExprAnd | ExprAndLeft }
    ExprOr = { ExprOrLeft ~ Or ~ ExprOrRight }
        ExprOrLeft = _{ ExprAndRight }
        Or = @{ ^"or" }
        ExprOrRight = _{ ExprOr | ExprOrLeft }

Value = _{ Row | Bool | Null | QuotedName | Number | String }
    Bool = @{ ^"true" | ^"false" }
    Null = @{ ^"null" }
    Number = @{ Int ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ Int)? }
        Int = @{ ("+" | "-")? ~ ASCII_DIGIT+ }
    String = @{ !(Keyword) ~ ('a'..'z' | "_" | Number)+ }
        Keyword = {
            ^"all" | ^"and" | ^"as" | ^"false" | ^"from"
            | ^"in" | ^"inner" | ^"insert" | ^"into" | ^"join" | ^"null"
            | ^"on" | ^"or" | ^"row" | ^"select" | ^"true" | ^"union"
            | ^"where" | ^"values"
        }
    QuotedName = @{ "\"" ~ String ~ "\"" ~ ("." ~ "\"" ~ String ~ "\"")? }
    Row = {
        ("(" ~ Value ~ ("," ~ Value)* ~ ")")
        | (^"row" ~ "(" ~ Value ~ ("," ~ Value)* ~ ")")
    }

EOF = { EOI | ";" }
WHITESPACE = _{ " " | "\t" | "\n" | "\n\r" }
