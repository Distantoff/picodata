Command = _{ SOI ~ Query ~ EOF }

Query = _{ UnionAll | Select | Values | Insert }
    Select = {
        ^"select" ~ Projection ~ ^"from" ~ Scan ~
        (((^"inner" ~ ^"join") | ^"join") ~ InnerJoin)? ~
        (^"on" ~ Condition)? ~ (^"where" ~ Selection)?
    }
        Projection = { Column ~ ("," ~ Column)*? }
            Column = { Row | Asterisk | Alias | Value }
                Alias = {Value ~ ^"as" ~ (String | QuotedName) }
                Asterisk = @{ "*" }
        Selection = { Expr* }
        Scan = { SubQuery | Table }
            Table = @{ String | QuotedName }
        InnerJoin = { Scan }
        Condition = { Selection }
    UnionAll = { (SubQuery | Select) ~ ^"union" ~ ^"all" ~ (UnionAll | SubQuery | Select) }
    SubQuery = { "(" ~ (UnionAll | Select) ~ ")" ~ (^"as" ~ (String | QuotedName))? }
    Insert = { ^"insert" ~ ^"into" ~ Table ~ Values }
    Values = { ^"values" ~ Row ~ ("," ~ Row)*?}

Expr = _{  Or | And | Cmp | Primary | Parentheses }
    Parentheses = _{ "(" ~ Expr ~ ")" }
    Primary = _{ SubQuery | Value }
    Cmp = _{ Eq | In | Gt | GtEq | Lt | LtEq | NotEq }
    Eq = { EqLeft ~ "=" ~ EqRight }
        EqLeft = _{ Primary | Parentheses }
        EqRight = _{ Eq | EqLeft }
    In = { InLeft ~ ^"in" ~ InRight }
        InLeft = _{ EqRight }
        InRight = _{ In | InLeft }
    Gt = { GtLeft ~ ">" ~ GtRight }
        GtLeft = _{ InRight }
        GtRight = _{ Gt | GtLeft }
    GtEq = { GtEqLeft ~ ">=" ~ GtEqRight }
        GtEqLeft = _{ GtRight }
        GtEqRight = _{ GtEq | GtEqLeft }
    Lt = { LtLeft ~ "<" ~ LtRight }
        LtLeft = _{ GtEqRight }
        LtRight = _{ Lt | LtLeft }
    LtEq = { LtEqLeft ~ "<=" ~ LtEqRight }
        LtEqLeft = _{ LtRight }
        LtEqRight = _{ LtEq | LtEqLeft }
    NotEq = { NotEqLeft ~ ("<>" | "!=") ~ NotEqRight }
        NotEqLeft = _{ LtEqRight }
        NotEqRight = _{ NotEq | NotEqLeft }  
    And = { AndLeft ~ ^"and" ~ AndRight }
        AndLeft = _{ Cmp | Primary | Parentheses }
        AndRight = _{ And | AndLeft }
    Or = { OrLeft ~ ^"or" ~ OrRight }
        OrLeft = _{ AndRight }
        OrRight = _{ Or | OrLeft }

Value = _{ Row | Bool | Null | QuotedName | Number | String }
    Bool = @{ ^"true" | ^"false" }
    Null = @{ ^"null" }
    Number = @{ Int ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ Int)? }
        Int = @{ ("+" | "-")? ~ ASCII_DIGIT+ }
    String = @{ !(Keyword) ~ ('a'..'z' | "_" | Number)+ }
        Keyword = {
            ^"all" | ^"and" | ^"as" | ^"false" | ^"from"
            | ^"in" | ^"inner" | ^"insert" | ^"into" | ^"join" | ^"null"
            | ^"on" | ^"or" | ^"row" | ^"select" | ^"true" | ^"union"
            | ^"where" | ^"values"
        }
    QuotedName = @{ "\"" ~ String ~ "\"" ~ ("." ~ "\"" ~ String ~ "\"")? }
    Row = {
        ("(" ~ Value ~ ("," ~ Value)* ~ ")")
        | (^"row" ~ "(" ~ Value ~ ("," ~ Value)* ~ ")")
    }

EOF = { EOI | ";" }
WHITESPACE = _{ " " | "\t" | "\n" | "\n\r" }
