Command = _{ SOI ~ (ExplainQuery | Query) ~ EOF }

ExplainQuery = _{ Explain }
    Explain = { ^"explain" ~ Query }

Query = _{ Except | UnionAll | Select | Values | Insert }
    Select = {
        ^"select" ~ Projection ~ ^"from" ~ Scan ~
        (((^"inner" ~ ^"join") | ^"join") ~ InnerJoin ~
        ^"on" ~ Condition)? ~ (^"where" ~ Selection)?
    }
        Projection = { ((Asterisk | Column) ~ ("," ~ (Asterisk | Column))*?) }
            Column = { Alias | Value }
                Alias = {Value ~ ^"as" ~ AliasName }
                AliasName = @{ Name }
                Reference = { (ScanName ~ "." ~ ColumnName) | ColumnName }
                    ColumnName = @{ Name }
                    ScanName = @{ Name }
                    Name = @{ NameString | ("\"" ~ NameString ~ "\"") }
                Asterisk = @{ "*" }
        Selection = { Expr }
        Scan = { (SubQuery | Table) ~ (^"as" ~ ScanName)? }
            Table = @{ Name }
        InnerJoin = { Scan }
        Condition = { Expr }
    UnionAll = { (SubQuery | Select) ~ ^"union" ~ ^"all" ~ (SubQuery | Select) }
    Except = { (SubQuery | Select) ~ ((^"except" ~ ^"distinct") | ^"except") ~ (SubQuery | Select) }
    SubQuery = { "(" ~ (Except | UnionAll | Select | Values) ~ ")" }
    Insert = { ^"insert" ~ ^"into" ~ Table ~ ("(" ~ TargetColumns ~ ")")? ~ (Values | Select) }
        TargetColumns = { ColumnName ~ ("," ~ ColumnName)* }
    Values = { ^"values" ~ ValuesRow ~ ("," ~ ValuesRow)*? }
        ValuesRow = { Row }

Expr = _{ Or | And | Unary | Between | Cmp | Primary | Parentheses }
    Parentheses = _{ "(" ~ Expr ~ ")" }
    Primary = _{ SubQuery | Value }
    Unary = _{ IsNull | IsNotNull}
        IsNull = { Primary ~ ^"is" ~ ^"null" }
        IsNotNull = { Primary ~ ^"is" ~ ^"not" ~ ^"null" }
    Cmp = _{ Eq | In | Gt | GtEq | Lt | LtEq | NotEq | NotIn }
    Eq = { EqLeft ~ "=" ~ EqRight }
        EqLeft = _{ Primary }
        EqRight = _{ Eq | EqLeft }
    In = { InLeft ~ ^"in" ~ InRight }
        InLeft = _{ Primary }
        InRight = _{ In | InLeft }
    Gt = { GtLeft ~ ">" ~ GtRight }
        GtLeft = _{ Primary }
        GtRight = _{ Gt | GtLeft }
    GtEq = { GtEqLeft ~ ">=" ~ GtEqRight }
        GtEqLeft = _{ Primary }
        GtEqRight = _{ GtEq | GtEqLeft }
    Lt = { LtLeft ~ "<" ~ LtRight }
        LtLeft = _{ Primary }
        LtRight = _{ Lt | LtLeft }
    LtEq = { LtEqLeft ~ "<=" ~ LtEqRight }
        LtEqLeft = _{ Primary }
        LtEqRight = _{ LtEq | LtEqLeft }
    NotEq = { NotEqLeft ~ ("<>" | "!=") ~ NotEqRight }
        NotEqLeft = _{ Primary }
        NotEqRight = _{ NotEq | NotEqLeft }
    NotIn = { NotInLeft ~ ^"not" ~ ^"in" ~ NotInRight }
        NotInLeft = _{ Primary }
        NotInRight = _{ NotIn | NotInLeft }
    Between = { BetweenLeft ~ ^"between" ~ BetweenCenter ~ ^"and" ~ BetweenRight }
        BetweenLeft = _{ Primary }
        BetweenCenter = _{ Primary }
        BetweenRight = _{ Primary }
    And = { AndLeft ~ ^"and" ~ AndRight }
        AndLeft = _{ Unary | Between | Cmp | Primary | Parentheses }
        AndRight = _{ And | AndLeft }
    Or = { OrLeft ~ ^"or" ~ OrRight }
        OrLeft = _{ AndRight }
        OrRight = _{ Or | OrLeft }

Function = { FunctionName ~ ("(" ~ FunctionArgs ~ ")") }
    FunctionName = @{ Name }
    FunctionArgs = _{ (FunctionExpr ~ ("," ~ FunctionExpr)*)? }
    FunctionExpr = _{ Primary | Parentheses }

Cast = { ^"cast" ~ "(" ~ Expr ~ ^"as" ~ Type ~ ")" }
    Type = _{ TypeAny | TypeBool | TypeDecimal | TypeDouble
             | TypeInt | TypeNumber | TypeScalar | TypeString
             | TypeText | TypeUnsigned | TypeVarchar }
    TypeAny = { ^"any" }
    TypeBool = { (^"boolean" | ^"bool") }
    TypeDecimal = { ^"decimal" }
    TypeDouble = { ^"double" }
    TypeInt = { (^"integer" | ^"int") }
    TypeNumber = { ^"number" }
    TypeScalar = { ^"scalar" }
    TypeString = { ^"string" }
    TypeText = { ^"text" }
    TypeUnsigned = { ^"unsigned" }
    TypeVarchar = { ^"varchar" ~ "(" ~ Length ~ ")" }
        Length = @{ Unsigned }

Concat = { ConcatLeft ~ ^"||" ~ ConcatRight }
    ConcatLeft = _{ SingleQuotedString | Cast | Function | Reference }
    ConcatRight = _{ Concat | ConcatLeft }


NameLetters = _{ ('А' .. 'Я' | 'а' .. 'я' | 'A' .. 'Z' | 'a'..'z' | "-" | "_") }
NameString = @{ !(WHITESPACE* ~ Keyword ~ WHITESPACE) ~ ((NameLetters ~ (NameLetters | ASCII_DIGIT)+) | NameLetters+) }
String = @{ !(WHITESPACE* ~ Keyword ~ WHITESPACE) ~ (Character | ("'" ~ "'") | "\"")* }
Keyword = { ^"except" | ^"union" | ^"where" }
Character = _{ LETTER | NUMBER | Other | OTHER_ALPHABETIC| Punctuation | SYMBOL }
Punctuation = _{
    CONNECTOR_PUNCTUATION
    | DASH_PUNCTUATION
    | OPEN_PUNCTUATION
    | CLOSE_PUNCTUATION
    | INITIAL_PUNCTUATION
    | "." | "?" | "!" | ":" | ";" | ","
}
Other = _{ "\\" | "/" | "@" | "%" | "&" | "*" | "#" | "§" | "»" | WHITESPACE }

Value = _{ Parameter | Row | True | False | Null | Decimal | Double | Unsigned | Integer | Concat | ConcatLeft }
    True = @{ ^"true" }
    False = @{ ^"false" }
    Null = @{ ^"null" }
    Decimal = @{ Integer ~ ("." ~ ASCII_DIGIT*) }
    Double = @{ Integer ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ Integer) }
    Integer = @{ ("+" | "-")? ~ ASCII_DIGIT+ }
    Unsigned = @{ ASCII_DIGIT+ }
    SingleQuotedString = _{ "'" ~ String ~ "'" }
    Row = {
        ("(" ~ Value ~ ("," ~ Value)* ~ ")")
        | (^"row" ~ "(" ~ Value ~ ("," ~ Value)* ~ ")")
    }
    Parameter = @{ "?" }

EOF = { EOI | ";" }
WHITESPACE = _{ " " | "\t" | "\n" | "\r\n" }
