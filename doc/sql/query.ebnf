statement   ::= explain | ddl | dml | dql | acl | call
explain     ::= 'EXPLAIN' (dml | dql)
dql         ::= (query | values)
                ('OPTION' '('
                    (('VTABLE_MAX_ROWS' | 'SQL_VDBE_MAX_STEPS') '=' unsigned)
                    (',' (('VTABLE_MAX_ROWS' | 'SQL_VDBE_MAX_STEPS') '=' unsigned))*
                ')')?
query       ::= ('WITH'
                cte ('(' column (',' column)* ')')? 'AS' '(' (select | values) ')'
                (',' cte ('(' column (',' column)* ')')? 'AS' '(' (select | values) ')')*
                )? select
select      ::= 'SELECT' 'DISTINCT'? projection (',' projection)* 'FROM' scan
                (( 'INNER' | 'LEFT' 'OUTER'? )? 'JOIN' scan 'ON' expression)*
                ('WHERE' expression)?
                ('GROUP' 'BY' expression (',' expression)*)?
                ('HAVING' expression)?
                ('ORDER' 'BY' expression ('ASC' | 'DESC')? (',' expression ('ASC' | 'DESC')?)*)?
                (('UNION' 'ALL'? | 'EXCEPT' 'DISTINCT'?) select)?
                (LIMIT (unsigned | "ALL" | "NULL"))?
projection  ::= (table '.')? '*' | expression (('AS')? name)? | aggregate
scan        ::= (table | '(' (query | values) ')') ('AS'? name)?
expression  ::= ('NOT'* (
                    (table '.')? column
                    | literal
                    | case
                    | cast
                    | current_date
                    | to_char
                    | to_date
                    | trim
                    | substr
                    | '(' expression ')'
                    | 'NOT'? 'EXISTS' '(' (query | values) ')'
                    | '(' (query | values) ')'
                    | '(' expression (',' expression)* ')'
                ) ('IS' 'NOT'? 'NULL')?)
                | expression
                (
                    'NOT'? 'BETWEEN' expression 'AND'
                    | ('+' | '-' | '*' | '/')
                    | ('<>' | '!=' | '>=' | '>' | '<=' | '<' | '=' | 'NOT'? 'IN')
                    | '||'
                    | 'AND'
                    | 'OR'
                )
                expression
aggregate   ::= ('AVG' | 'COUNT' | 'MAX' | 'MIN' | 'SUM' | 'TOTAL') '(' ('DISTINCT'?  expression) ')'
               | 'GROUP_CONCAT' '(' expression ','  "'" string "'" ')'
case        ::= 'CASE' expression?
                ('WHEN' expression 'THEN' expression)+
                ('ELSE' expression)? 'END'
cast        ::= ('CAST' '(' expression 'AS' type ')') | (experssion "::" type)
to_char     ::= 'TO_CHAR' '(' expression ',' format ')'
to_date     ::= 'TO_DATE' '(' expression ',' format ')'
trim        ::= 'TRIM' '('
                ((('LEADING' | 'TRAILING' | 'BOTH')? removal_chars
                | ('LEADING' | 'TRAILING' | 'BOTH')) 'FROM')? string ')'
substr      ::= 'SUBSTR' '(' string ',' from (',' count)? ')'
values      ::= 'VALUES'
                ('(' (expression(',' expression)*) ')')
                (',' ('(' (expression(',' expression)*) ')'))*
literal     ::= 'TRUE'
                | 'FALSE'
                | 'NULL'
                | decimal
                | double
                | integer
                | string
                | unsigned
                | ('$' unsigned)
                | '?'
dml         ::= (call | delete | insert | update)
                ('OPTION' '('
                    (('VTABLE_MAX_ROWS' | 'SQL_VDBE_MAX_STEPS') '=' unsigned)
                    (',' (('VTABLE_MAX_ROWS' | 'SQL_VDBE_MAX_STEPS') '=' unsigned))*
                ')')?
call        ::= 'CALL' procedure '(' (literal (',' literal)*)? ')'
delete      ::= 'DELETE' 'FROM' table ('WHERE' expression)?
insert      ::= 'INSERT' 'INTO' table ('(' column (',' column)* ')')? (query | values)
                ('ON' 'CONFLICT' 'DO'  ('NOTHING' | 'REPLACE' | 'FAIL'))?
update      ::= 'UPDATE' table 'SET'
                column '=' expression (',' column '=' expression)*
                ('FROM' scan)? ('WHERE' expression)?
acl         ::= (alter_user | create_role | create_user | drop_role | drop_user | grant | revoke)
                ('OPTION' '(' ('TIMEOUT' '=' double)')')?
revoke      ::= 'REVOKE' (
                    (('CREATE' | 'ALTER' | 'DROP') 'USER')
                    | (('ALTER' | 'DROP') 'ON' 'USER' user)
                    | (('CREATE' | 'DROP') 'ROLE')
                    | ('DROP' 'ON' 'ROLE' role)
                    | (('CREATE' | 'ALTER' | 'DROP' | 'READ' | 'WRITE') 'TABLE')
                    | (('ALTER' | 'DROP' | 'READ' | 'WRITE') 'ON' 'TABLE' table)
                    | (('CREATE' | 'EXECUTE' | 'DROP') 'PROCEDURE')
                    | (('EXECUTE' | 'DROP') 'ON' 'PROCEDURE' procedure ('(' type (',' type)* ')')?)
                    | role
                )
                'FROM' (role | user)
grant       ::= 'GRANT' (
                    (('CREATE' | 'ALTER' | 'DROP') 'USER')
                    | (('ALTER' | 'DROP') 'ON' 'USER' user)
                    | (('CREATE' | 'DROP') 'ROLE')
                    | ('DROP' 'ON' 'ROLE' role)
                    | (('CREATE' | 'ALTER' | 'DROP' | 'READ' | 'WRITE') 'TABLE')
                    | (('ALTER' | 'DROP' | 'READ' | 'WRITE') 'ON' 'TABLE' table)
                    | (('CREATE' | 'EXECUTE' | 'DROP') 'PROCEDURE')
                    | (('EXECUTE' | 'DROP') 'ON' 'PROCEDURE' procedure ('(' type (',' type)* ')')?)
                    | role
                )
                'TO' (role | user)
ddl         ::= (alter_procedure | create_index | create_procedure | create_table
                | drop_index | drop_procedure | drop_table)
                ('OPTION' '(' ('TIMEOUT' '=' double)')')?
alter_procedure ::= 'ALTER' 'PROCEDURE' procedure ('(' type (',' type)* ')')?
                     'RENAME' 'TO' procedure
create_index ::= 'CREATE' 'UNIQUE'? 'INDEX' index 'ON' table
                 ('USING' ('TREE' | 'HASH' | 'RTREE' | 'BITSET'))?
                 '(' column (',' column)* ')' ('WITH' '('
                     (
                         ('BLOOM_FPR' '=' decimal)
                         | ('PAGE_SIZE' '=' unsigned)
                         | ('RANGE_SIZE' '=' unsigned)
                         | ('RUN_COUNT_PER_LEVEL' '=' unsigned)
                         | ('RUN_SIZE_RATIO' '=' decimal)
                         | ('DIMENSION' '=' unsigned)
                         | ('DISTANCE' '=' ('EUCLID' | 'MANHATTAN'))
                         | ('HINT' '=' ('TRUE' | 'FALSE'))
                     )
                     (
                        ','
                         (
                             ('BLOOM_FPR' '=' decimal)
                             | ('PAGE_SIZE' '=' unsigned)
                             | ('RANGE_SIZE' '=' unsigned)
                             | ('RUN_COUNT_PER_LEVEL' '=' unsigned)
                             | ('RUN_SIZE_RATIO' '=' decimal)
                             | ('DIMENSION' '=' unsigned)
                             | ('DISTANCE' '=' ('EUCLID' | 'MANHATTAN'))
                             | ('HINT' '=' ('TRUE' | 'FALSE'))
                         )
                     )*
                 ')')?
create_procedure ::= 'CREATE' 'PROCEDURE' procedure '(' type (',' type)* ')'
                     ('LANGUAGE' 'SQL')? (
                         ('AS' '$$' (insert | update | delete) '$$')
                         | ('BEGIN' 'ATOMIC' (insert | update | delete) 'END')
                     )
create_role    ::= 'CREATE' 'ROLE' role
create_table   ::= 'CREATE' 'TABLE' table
                   '('
                       column type ('NOT'? 'NULL')? ('PRIMARY' 'KEY')? (',' column type ('NOT'? 'NULL')? ('PRIMARY' 'KEY')?)*
                       (',' 'PRIMARY' 'KEY' '(' column (',' column)* ')')?
                   ')'
                   ('USING' ('MEMTX' | 'VINYL'))?
                   ('DISTRIBUTED' (('BY' '(' column (',' column)*  ')' ('IN' 'TIER' tier)?) | 'GLOBALLY'))?
create_user    ::= 'CREATE' 'USER' user 'WITH'? 'PASSWORD' "'" password "'"
                   ('USING' ('CHAP-SHA1' | 'LDAP' | 'MD5'))?
alter_user     ::= 'ALTER' 'USER' user
                   'WITH'? (
                       'LOGIN'
                       | 'NOLOGIN'
                       | 'PASSWORD' "'" password "'" ('USING' ('CHAP-SHA1' | 'LDAP' | 'MD5'))?
                       | 'RENAME' 'TO' user
                   )
drop_index     ::= 'DROP' 'INDEX' index
drop_procedure ::= 'DROP' 'PROCEDURE' procedure ('(' type (',' type)* ')')?
drop_table     ::= 'DROP' 'TABLE' table
drop_role      ::= 'DROP' 'ROLE' role
drop_user      ::= 'DROP' 'USER' user
type        ::= 'BOOL'
                | 'BOOLEAN'
                | 'DATETIME'
                | 'DECIMAL'
                | 'DOUBLE'
                | 'INT'
                | 'INTEGER'
                | 'NUMBER'
                | 'SCALAR'
                | 'STRING'
                | 'TEXT'
                | 'UNSIGNED'
                | 'UUID'
                | 'VARCHAR' '(' length ')'
