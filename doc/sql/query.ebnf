STATEMENT   ::= EXPLAIN | DDL | DML | DQL | ACL
EXPLAIN     ::= 'EXPLAIN' ( DML | DQL )
DQL         ::= (SELECT | SELECT UNION ALL SELECT | SELECT (EXCEPT 'DISTINCT'? ) SELECT ) Options?
DML         ::= (DELETE | INSERT) Options?
DDL         ::= CreateTable | DropTable
ACL         ::= DropUser
CreateTable ::= 'create table' table '(' Column (',' Column)* ',' PrimaryKey ')' ('using' ('memtx' | 'vinyl'))? Distribution ('option' '(' ('timeout' '=' DOUBLE)')')?
Column      ::= name ('Bool' | 'Decimal' | 'Double' | 'Int' | 'Number' | 'Scalar' | 'String' | 'Text' | 'Unsigned' | 'Varchar') ('null' | 'not null')?
PrimaryKey  ::= 'primary key' '(' name (',' name)* ')'
Distribution ::= 'global' | ('distributed by' '(' name (',' name)*  ')')
DropTable   ::= 'drop table' table ('option' '(' ('timeout' '=' DOUBLE)')')?
DropUser    ::= 'drop user' user ('option' '(' ('timeout' '=' DOUBLE)')')?
Options     ::= 'option' '(' ('vtable_max_rows' '=' UNSIGNED)? (',' 'sql_vdbe_max_steps' '=' UNSIGNED)? ')'
SELECT      ::= 'SELECT' ((column (',' column)*) ) 'FROM' (table ('AS' name)? | '(' (SELECT | VALUES) ')' ('AS' name)?) ( ('INNER')? 'JOIN' (table ('AS' name)? | ('(' (SELECT | VALUES) ')' ('AS' name)?))  'ON' expression )? ( 'WHERE' expression )? ( 'GROUP BY' expression(',' expression)* )?
VALUES      ::= 'VALUES' '(' row (',' row)* ')'
row         ::= '(' (value (',' value)*) ')'
column      ::= '*' | (table '.')? name ('AS' name)? |  table '.' '*' | expression ('AS' name)?
expression  ::= column_name
               | 'CAST' '(' expression 'AS' type ')'
               | expression ('IS NULL' | 'IS NOT NULL')
               | expression ('OR' | 'AND' | '*' | '/' | '+' | '-' | '=' | '>' | '<' | '>=' | '<=' | ('<>' | '!=')) expression
               | expression 'BETWEEN' expression 'AND' expression
               | reference
               | SELECT
               | value
reference   ::= (table '.')? name
value       ::= 'TRUE'
                | 'FALSE'
                | 'NULL'
                | '?'
                | INTEGER
                | UNSIGNED
                | DOUBLE
                | DECIMAL
                | STRING
                | row
type        ::= 'ANY'
                | 'BOOL'
                | 'BOOLEAN'
                | 'DECIMAL'
                | 'DOUBLE'
                | 'INT'
                | 'INTEGER'
                | 'NUMBER'
                | 'SCALAR'
                | 'STRING'
                | 'TEXT'
                | 'UNSIGNED'
                | 'VARCHAR' ('(' length ')')?
DELETE      ::= 'DELETE' 'FROM' table ('WHERE' expression)?
INSERT      ::= 'INSERT' 'INTO' table ('(' name (',' name)* ')')? (VALUES row | SELECT )  ('on conflict' 'do'  ('nothing' | 'replace' | 'fail'))?

