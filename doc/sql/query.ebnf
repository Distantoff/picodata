statement   ::= explain | ddl | dml | dql | acl | call
explain     ::= 'EXPLAIN' ( dml | dql )

dql         ::= (cte | values)
                ( 'OPTION' '('
                    (('VTABLE_MAX_ROWS' | 'SQL_VDBE_MAX_STEPS') '=' unsigned)
                    (',' (('VTABLE_MAX_ROWS' | 'SQL_VDBE_MAX_STEPS') '=' unsigned))*
                ')')?
cte         ::= ('WITH'
                cte ('(' column (',' column)* ')')? 'AS' '(' (select | values) ')'
                (',' cte ('(' column (',' column)* ')')? 'AS' '(' (select | values) ')')*
                )? select
select      ::= 'SELECT' 'DISTINCT'? projection (',' projection)* 'FROM' scan
                (( 'INNER' | 'LEFT' 'OUTER'? )? 'JOIN' scan 'ON' expression)*
                ('WHERE' expression)?
                ('GROUP' 'BY' expression (',' expression)*)?
                ('HAVING' expression)?
                ('ORDER' 'BY' expression ('ASC' | 'DESC')? (',' expression ('ASC' | 'DESC')?)*)?
                (('UNION' 'ALL'? | 'EXCEPT' 'DISTINCT'?) select)?
projection  ::= (table '.')? '*' | expression (('AS')? name)? | aggregate
scan        ::= (table | '(' (select | values) ')') ('AS'? name)?
expression  ::= (table '.')? column
               | expression ('IS' ('NOT')? 'NULL')
               | expression ('OR' | 'AND' | '*' | '/' | '+' | '-' | '=' | '>' | '<' | '>=' | '<=' | ('<>' | '!=')) expression
               | expression ('NOT')? 'BETWEEN' expression 'AND' expression
               | ('NOT')? 'EXISTS' '(' (select | values) ')'
               | expression ('NOT')? 'IN' '(' (select | values) ')'
               | (table '.')? column
               | '(' (select | values) ')'
               | literal
               | cast
               | ('(' (expression(',' expression)*) ')')
               | 'NOT' expression
               | '(' expression ')'
               | 'CURRENT_DATE'
               | 'TO_DATE' '(' expression',' format ')'
               | 'TO_CHAR' '(' expression',' format ')'
               | 'TRIM' '(' ((('LEADING' | 'TRAILING' | 'BOTH')? expression) | ('LEADING' | 'TRAILING' | 'BOTH')) 'FROM' expression ')'
               | 'CASE' expression? ('WHEN' expression 'THEN' expression)+ ('ELSE' expression)? 'END'
aggregate   ::= ('AVG' | 'COUNT' | 'MAX' | 'MIN' | 'SUM' | 'TOTAL') '(' ( 'DISTINCT'?  expression ) ')'
               | 'GROUP_CONCAT' '(' expression ','  "'" string "'" ')'
cast        ::= 'CAST' '(' expression 'AS' type ')'
values      ::= 'VALUES'
                ('(' (expression(',' expression)*) ')')
                (',' ('(' (expression(',' expression)*) ')'))*
literal     ::= 'TRUE'
                | 'FALSE'
                | 'NULL'
                | '?'
                | ('$' unsigned)
                | integer
                | unsigned
                | double
                | decimal
                | string

dml         ::= (call | delete | insert | update)
                ( 'OPTION' '('
                    (('VTABLE_MAX_ROWS' | 'SQL_VDBE_MAX_STEPS') '=' unsigned)
                    (',' (('VTABLE_MAX_ROWS' | 'SQL_VDBE_MAX_STEPS') '=' unsigned))*
                ')')?
call        ::= 'CALL' procedure '(' literal? (',' literal)* ')'
delete      ::= 'DELETE' 'FROM' table ('WHERE' expression)?
insert      ::= 'INSERT' 'INTO' table ('(' column (',' column)* ')')? (values | select)
                ('ON' 'CONFLICT' 'DO'  ('NOTHING' | 'REPLACE' | 'FAIL'))?
update      ::= 'UPDATE' table 'SET'
                column '=' expression (',' column '=' expression)*
                ('FROM' scan)? ('WHERE' expression)?

acl         ::= (alter_user | create_role | create_user | drop_role | drop_user | grant | revoke)
                ('OPTION' '(' ('TIMEOUT' '=' double)')')?
revoke      ::= 'REVOKE' (
                    (('CREATE' | 'ALTER' | 'DROP') 'USER')
                    | (('ALTER' | 'DROP') 'ON' 'USER' user)
                    | (('CREATE' | 'DROP') 'ROLE')
                    | ('DROP' 'ON' 'ROLE' role)
                    | (('CREATE' | 'ALTER' | 'DROP' | 'READ' | 'WRITE') 'TABLE')
                    | (('ALTER' | 'DROP' | 'READ' | 'WRITE') 'ON' 'TABLE' table)
                    | (('CREATE' | 'EXECUTE' | 'DROP') 'PROCEDURE')
                    | (('EXECUTE' | 'DROP') 'ON' 'PROCEDURE' procedure ('(' type (',' type)* ')')?)
                    | role
                )
                'FROM' (role | user)
grant       ::= 'GRANT' (
                    (('CREATE' | 'ALTER' | 'DROP') 'USER')
                    | (('ALTER' | 'DROP') 'ON' 'USER' user)
                    | (('CREATE' | 'DROP') 'ROLE')
                    | ('DROP' 'ON' 'ROLE' role)
                    | (('CREATE' | 'ALTER' | 'DROP' | 'READ' | 'WRITE') 'TABLE')
                    | (('ALTER' | 'DROP' | 'READ' | 'WRITE') 'ON' 'TABLE' table)
                    | (('CREATE' | 'EXECUTE' | 'DROP') 'PROCEDURE')
                    | (('EXECUTE' | 'DROP') 'ON' 'PROCEDURE' procedure ('(' type (',' type)* ')')?)
                    | role
                )
                'TO' (role | user)

ddl         ::= (alter_procedure | create_index | create_procedure | create_table
                | drop_index | drop_procedure | drop_table)
                ('OPTION' '(' ('TIMEOUT' '=' double)')')?

alter_procedure ::= 'ALTER' 'PROCEDURE' procedure ('(' type (',' type)* ')')?
                     'RENAME' 'TO' procedure
create_index ::= 'CREATE' 'UNIQUE'? 'INDEX' index 'ON' table
                 ('USING' ('TREE' | 'HASH' | 'RTREE' | 'BITSET'))?
                 '(' column (',' column)* ')' ('WITH' '('
                     (
                         ('BLOOM_FPR' '=' decimal)
                         | ('PAGE_SIZE' '=' unsigned)
                         | ('RANGE_SIZE' '=' unsigned)
                         | ('RUN_COUNT_PER_LEVEL' '=' unsigned)
                         | ('RUN_SIZE_RATIO' '=' decimal)
                         | ('DIMENSION' '=' unsigned)
                         | ('DISTANCE' '=' ('EUCLID' | 'MANHATTAN'))
                         | ('HINT' '=' (TRUE | FALSE))
                     )
                     (
                        ','
                         (
                             ('BLOOM_FPR' '=' decimal)
                             | ('PAGE_SIZE' '=' unsigned)
                             | ('RANGE_SIZE' '=' unsigned)
                             | ('RUN_COUNT_PER_LEVEL' '=' unsigned)
                             | ('RUN_SIZE_RATIO' '=' decimal)
                             | ('DIMENSION' '=' unsigned)
                             | ('DISTANCE' '=' ('EUCLID' | 'MANHATTAN'))
                             | ('HINT' '=' (TRUE | FALSE))
                         )
                     )*
                 ')')?
create_procedure ::= 'CREATE' 'PROCEDURE' procedure '(' type (',' type)* ')'
                     ('language' 'SQL')? (
                         ('as' '$$' (insert | update | delete) '$$')
                         | ('begin' 'atomic' (insert | update | delete) 'end')
                     )
create_role    ::= 'CREATE' 'ROLE' role
create_table   ::= 'CREATE' 'TABLE' table
                   '('
                       column type ('NOT'? 'NULL')? ('PRIMARY' 'KEY')? (',' column type ('NOT'? 'NULL')? ('PRIMARY' 'KEY')?)*
                       (',' 'PRIMARY' 'KEY' '(' column (',' column)* ')')?
                   ')'
                   ('USING' ('MEMTX' | 'VINYL'))?
                   ('DISTRIBUTED' (('BY' '(' column (',' column)*  ')' ('IN' 'TIER' tier)?) | 'GLOBALLY'))?
create_user    ::= 'CREATE' 'USER' user 'WITH'? 'PASSWORD' "'" password "'"
                   ('USING' ('CHAP-SHA1' | 'LDAP' | 'MD5'))?
alter_user     ::= 'ALTER' 'USER' user
                   'WITH'? (
                       'LOGIN'
                       | 'NOLOGIN'
                       | 'PASSWORD' "'" password "'" ('USING' ('CHAP-SHA1' | 'LDAP' | 'MD5'))?
                       | 'RENAME' 'TO' user
                   )
drop_index     ::= 'DROP' 'INDEX' index
drop_procedure ::= 'DROP' 'PROCEDURE' procedure ('(' type (',' type)* ')')?
drop_table     ::= 'DROP' 'TABLE' table
drop_role      ::= 'DROP' 'ROLE' role
drop_user      ::= 'DROP' 'USER' user
type        ::= ('BOOL' | 'BOOLEAN')
                | 'DATETIME'
                | 'DECIMAL'
                | 'DOUBLE'
                | ('INT' | 'INTEGER')
                | 'NUMBER'
                | 'SCALAR'
                | ('STRING' | 'TEXT')
                | 'UNSIGNED'
                | 'UUID'
                | 'VARCHAR' ('(' length ')')?
